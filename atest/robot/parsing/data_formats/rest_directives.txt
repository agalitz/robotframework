*** Settings ***
Suite Setup     Run keywords
...             Check is docutils installed
...             Check is Pygments installed
Test Setup      Run keywords
...             Make test non-critical and fail it if docutils is not installed
...             Make test non-critical and fail it if Pygments is not installed
Force Tags      regression  jybot  pybot
Resource        formats_resource.txt

*** Test Cases ***

One ReST using code-directive
    [Setup]
    ${status}  ${msg} =  Run Keyword And Ignore Error  Run sample file and check tests  parsing${/}data_formats${/}rest_directives${/}sample.rst
    Run Keyword If  "${DOCUTILS INSTALLED}" == "YES" and "${PYGMENTS_INSTALLED}" == "YES" and "${status}" == "FAIL"  FAIL  ${msg}
    Run Keyword Unless  "${DOCUTILS INSTALLED}" == "YES"  Clear error should be given when docutils is not installed
    Run Keyword Unless  "${PYGMENTS INSTALLED}" == "YES"  Clear error should be given when Pygments is not installed

*** Keywords ***

Clear Error Should Be Given When Docutils Is Not Installed
    ${stderr} =  Get Stderr
    Should Match RegExp  ${stderr}  \\[ ERROR \\] Parsing '.*[/\\\\]sample.rst' failed: Using reStructuredText test data requires having 'docutils' module installed.\n \n Try --help for usage information.\n

Clear Error Should Be Given When Pygments Is Not Installed
    ${stderr} =  Get Stderr
    Should Match RegExp  ${stderr}  \\[ ERROR \\] Parsing '.*[/\\\\]sample.rst' failed: Using reStructuredText test data using '.. code:: robotframework'-directives requires having 'Pygments' module installed.\n \n Try --help for usage information.\n
